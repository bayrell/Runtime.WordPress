/*!
 *  BayLang Technology
 *
 *  (c) Copyright 2016-2024 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Runtime.WordPress.Admin.Api;

use Runtime.re;
use Runtime.fs;
use Runtime.lib;
use Runtime.Exceptions.ApiError;
use Runtime.Exceptions.RuntimeException;
use Runtime.Web.Annotations.ApiMethod;
use Runtime.Widget.Crud.SaveApi;
use Runtime.Widget.Crud.Rules.CrudRule;
use Runtime.Widget.Crud.Rules.MatchRule;
use Runtime.Widget.Crud.Rules.Required;
use BayLang.Constructor.Backend.ApiHook;
use BayLang.Helper.Project;


class ProjectSaveApi extends SaveApi
{
	/**
	 * Returns api name
	 */
	static string getApiName() => "admin.wordpress.project";
	
	
	/**
	 * Returns rules
	 */
	Vector<CrudRule> getRules() =>
	[
		new MatchRule{"name": "api_name", "regular": MatchRule::ALPHA_NUMERIC_DASH},
		new Required{"name": "api_name", "check_update": false},
		new Required{"name": "name"},
	];
	
	
	/**
	 * Returns item fields
	 */
	Collection getItemFields() =>
	[
		"id",
		"name",
		"description",
	];
	
	
	/**
	 * Returns save fields
	 */
	Collection getSaveFields() =>
	[
		"api_name",
		"name",
		"description",
	];
	
	
	/**
	 * New item
	 */
	async void newItem() => new Project(null);
	
	
	/**
	 * Find item
	 */
	async void findItem(Dict pk)
	{
		string project_id = pk.get("id");
		return await ApiHook::getProject(project_id);
	}
	
	
	/**
	 * Reload cache
	 */
	@ApiMethod
	async void reloadCache()
	{
		/* Load data */
		await this.loadItem(false);
		
		/* Project path */
		string project_path = this.item.getPath();
		string cache_path = fs::join([project_path, ".cache"]);
		
		/* Remove cache */
		#ifcode PHP then
		shell_exec("rm -rf $cache_path");
		#endif
		
		/* Load project */
		await this.item.load();
		
		/* Success */
		this.success();
	}
	
	
	/**
	 * Action create
	 */
	@ApiMethod
	async void actionCreate()
	{
		/* Load data */
		await this.loadData();
		
		/* Theme name */
		string api_name = this.data.get("api_name");
		string theme_name = this.data.get("name");
		string theme_root_path = "";
		string file_archive = "";
		
		#ifcode PHP then
		$theme_root_path = get_theme_root();
		$file_archive = \BayLang_Plugin::getPath() . "/files/clear-wp-theme.zip";
		#endif
		
		/* Check theme is exists */
		string theme_path = fs::join([theme_root_path, api_name]);
		if (await fs::exists(theme_path))
		{
			this.fields.addFieldError("api_name", "Api name is exists");
		}
		
		/* Check fields error */
		this.fields.checkError();
		
		/* Create theme path */
		if (not await fs::exists(theme_path))
		{
			await fs::mkdir(theme_path);
		}
		
		/* Copy project */
		#ifcode PHP then
		shell_exec("unzip $file_archive -d $theme_path");
		#endif
		
		/* Save style.css */
		string style_css_path = fs::join([theme_path, "style.css"]);
		string style_css_content = rs::join("\n", [
			"/*",
			" * Theme Name: " ~ theme_name,
			" */",
		]);
		await fs::saveFile(style_css_path, style_css_content);
		
		/* Success */
		this.success();
	}
	
	
	/**
	 * Action item
	 */
	@ApiMethod
	async void actionItem()
	{
		/* Load data */
		await this.loadItem(false);
		
		/* Get domains */
		Collection<string> domains = [];
		if (this.item.info.has("domains"))
		{
			domains = this.item.info.get("domains");
		}
		
		/* Success */
		this.success({
			"data":
			{
				"item":
				{
					"id": this.item.getID(),
					"name": this.item.getName(),
					"description": this.item.getDescription(),
					"domains": domains,
				},
			},
		});
	}
	
	
	/**
	 * Action save
	 */
	@ApiMethod
	async void actionSave()
	{
		/* Load data */
		await this.loadItem();
		await this.loadData();
		
		/* Theme settings */
		string theme_name = "";
		string theme_description = "";
		
		/* Set project info */
		if (this.data.has("name"))
		{
			theme_name = this.data.get("name");
			this.item.setName(theme_name);
		}
		if (this.data.has("description"))
		{
			theme_description = this.data.get("description");
			this.item.setDescription(theme_description);
		}
		
		/* Save project */
		await this.item.saveInfo();
		
		/* Trim name and description */
		theme_name = rs::trim(theme_name);
		theme_description = rs::trim(theme_description);
		theme_description = re::replace("[\n\r]", " ", theme_description);
		theme_description = re::replace("\s+", " ", theme_description);
		
		/* Save wordpress */
		string style_css_path = fs::join([this.item.path, "style.css"]);
		string style_css_content = rs::join("\n", [
			"/*",
			" * Theme Name: " ~ theme_name,
			" * Description: " ~ theme_description,
			" */",
		]);
		await fs::saveFile(style_css_path, style_css_content);
		
		/* Success */
		this.success();
	}
}
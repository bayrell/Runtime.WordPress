/*!
 *  BayLang Technology
 *
 *  (c) Copyright 2016-2024 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Runtime.WordPress.Admin.FormSettings;

use Runtime.Exceptions.ApiError;
use Runtime.Exceptions.ItemNotFound;
use Runtime.Serializer;
use Runtime.Serializer.BaseType;
use Runtime.Serializer.BooleanType;
use Runtime.Serializer.IntegerType;
use Runtime.Serializer.ObjectType;
use Runtime.Serializer.MapType;
use Runtime.Serializer.Required;
use Runtime.Serializer.StringType;
use Runtime.Serializer.VectorType;
use Runtime.ORM.Query;
use Runtime.Web.ApiRequest;
use Runtime.Web.ApiResult;
use Runtime.Web.Annotations.ApiMethod;
use Runtime.Widget.Api.SaveApi;
use Runtime.Widget.Api.Rules.ReadOnlyRule;
use Runtime.WordPress.Database.Form;
use Runtime.WordPress.Admin.FormSettings.FormItem;


class FormSaveApi extends SaveApi
{
	/**
	 * Returns api name
	 */
	static string getApiName() => "admin.wordpress.forms.settings.save";
	
	
	/**
	 * Returns relation name
	 */
	string getRealtionName() => classof Form;
	
	
	/**
	 * Returns save rules
	 */
	Vector rules() => [];
	
	
	/**
	 * Returns serialize rules for pk
	 */
	BaseType getPrimaryRules() => new MapType{
		"id": new IntegerType()
	};
	
	
	/**
	 * Returns serialize rules
	 */
	BaseType getType() => new MapType{
		"id": new IntegerType(),
		"name": new StringType(),
		"api_name": new StringType(),
		"email_to": new StringType(),
		"settings": new MapType{
			"fields": new VectorType(new MapType{
				"name": new StringType(),
				"type": new StringType(),
				"title": new StringType(),
				"placeholder": new StringType(),
				"required": new StringType(),
			})
		}
	};
	
	
	/**
	 * Returns item fields
	 */
	Collection getItemFields() =>
	[
		"id",
		"name",
		"api_name",
		"settings",
		"email_to",
	];
	
	
	/**
	 * Build query
	 */
	async void buildQuery(Query q)
	{
	}
	
	
	/**
	 * Before save
	 */
	async void onSaveBefore()
	{
		/* Set settings */
		Map settings = this.item.get("settings");
		if (settings == null) this.item.set("settings", {});
	}
	
	
	/**
	 * Save form
	 */
	@ApiMethod{ "name": "save" }
	static async ApiResult actionSave(ApiRequest request)
	{
		FormSaveApi api = new FormSaveApi{
			"action": "save",
			"request": request,
		};
		
		/* Set primary key */
		Map pk = request.get("pk");
		if (pk) api.setPrimaryKey(pk);
		
		/* Set data */
		Map item = request.get("item");
		api.setData(item);
		
		/* Find item */
		await api.findOrCreate();
		
		/* Save item */
		await api.save();
		api.setResult();
		
		return api.success();
	}
	
	
	/**
	 * Delete form
	 */
	@ApiMethod{ "name": "delete" }
	static async ApiResult actionDelete(ApiRequest request)
	{
		FormSaveApi api = new FormSaveApi{
			"action": "delete",
			"request": request,
		};
		
		/* Set primary key */
		Map pk = request.get("pk");
		api.setPrimaryKey(pk);
		
		/* Find item */
		await api.findOrCreate();
		
		/* Delete item */
		await api.delete();
		
		return api.success();
	}
}
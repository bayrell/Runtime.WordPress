/*!
 *  BayLang Technology
 *
 *  (c) Copyright 2016-2024 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Runtime.WordPress.Admin.MailSettings;

use Runtime.Serializer.BaseType;
use Runtime.Serializer.IntegerType;
use Runtime.Serializer.MapType;
use Runtime.Serializer.Required;
use Runtime.Serializer.StringType;
use Runtime.ORM.Query;
use Runtime.Web.ApiResult;
use Runtime.Web.ApiRequest;
use Runtime.Web.Annotations.ApiMethod;
use Runtime.Widget.Api.SaveApi;
use Runtime.WordPress.Database.MailSettings;


class MailSaveApi extends SaveApi
{
	/**
	 * Returns api name
	 */
	pure string getApiName() => "admin.wordpress.mail.settings.save";
	
	
	/**
	 * Returns relation name
	 */
	string getRealtionName() => classof MailSettings;
	
	
	/**
	 * Returns save rules
	 */
	Vector rules() => [];
	
	
	/**
	 * Returns serialize rules for pk
	 */
	BaseType getPrimaryRules() => new MapType{
		"id": new IntegerType(),
	}
	
	
	/**
	 * Returns serialize rules
	 */
	BaseType getType() =>
		new MapType{
			"enable": new IntegerType(),
			"plan": [new Required(), new StringType()],
			"host": new StringType(),
			"port": new StringType(),
			"login": new StringType(),
			"password": new StringType(),
			"ssl_enable": new IntegerType(),
		}
	;
	
	
	/**
	 * Returns item fields
	 */
	Collection getItemFields() =>
	[
		"id",
		"enable",
		"plan",
		"host",
		"port",
		"login",
		"password",
		"ssl_enable",
	];
	
	
	/**
	 * Build query
	 */
	async void buildQuery(Query q)
	{
	}
	
	
	/**
	 * Action save
	 */
	@ApiMethod{ "name": "save" }
	async static ApiResult actionSave(ApiRequest request)
	{
		MailSaveApi api = new MailSaveApi{
			"action": "save",
			"request": request,
		};
		
		/* Set primary key */
		Map pk = request.get("pk");
		if (pk) api.setPrimaryKey(pk);
		
		/* Set data */
		Map item = request.get("item");
		api.setData(item);
		
		/* Find item */
		await api.findOrCreate();
		
		/* Save item */
		await api.save();
		api.setResult();
		
		return api.success();
	}
	
	
	/**
	 * Action delete
	 */
	@ApiMethod{ "name": "delete" }
	async static ApiResult actionDelete(ApiRequest request)
	{
		MailSaveApi api = new MailSaveApi{
			"action": "delete",
			"request": request,
		};
		
		/* Set primary key */
		Map pk = request.get("pk");
		api.setPrimaryKey(pk);
		
		/* Find item */
		await api.findOrCreate();
		
		/* Delete item */
		await api.delete();
		
		return api.success();
	}
}
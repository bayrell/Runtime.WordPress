/*!
 *  BayLang Technology
 *
 *  (c) Copyright 2016-2024 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Runtime.WordPress.Database;

use Runtime.BaseObject;
use Runtime.Serializer.BaseType;
use Runtime.Serializer.MapType;
use Runtime.Serializer.Required;
use Runtime.ORM.Relation;
use Runtime.ORM.Record;
use Runtime.ORM.Annotations.AutoIncrement;
use Runtime.ORM.Annotations.BigIntType;
use Runtime.ORM.Annotations.BooleanType;
use Runtime.ORM.Annotations.JsonType;
use Runtime.ORM.Annotations.Primary;
use Runtime.ORM.Annotations.StringType;


class Form extends Relation
{
    /**
     * Returns table name
     */
    pure string getTableName() => "forms";
    
    
    /**
     * Returns table schema
     */
    pure memorize Collection<BaseObject> schema() =>
	[
		/* Fields */
		new BigIntType{"name": "id"},
		new StringType{"name": "name"},
		new StringType{"name": "api_name"},
		new JsonType{"name": "settings"},
		new StringType{"name": "email_to"},
		new BooleanType{"name": "is_deleted"},
		
		/* Index */
		new AutoIncrement{"name": "id"},
		new Primary{"keys": ["id"]},
	];
	
	
	/**
	 * Find form
	 */
	async Record<Form> findForm(string form_name)
	{
		return await this.fetchRecord(
			this.select().where("api_name", "=", form_name)
		);
	}
	
	
	/**
	 * Returns rules
	 */
	BaseType getRules(Record<Form> form)
	{
		Map settings = form.get("settings");
		Vector fields = settings.get("fields");
		
		BaseType rules = new MapType();
		for (int i=0; i<fields.count(); i++)
		{
			Map field = fields.get(i);
			string field_name = field.get("name");
			string field_type = field.get("type");
			if (field_type == "input" or field_type == "textarea")
			{
				rules.addType(field_name, new StringType());
			}
			bool field_required = rtl::toInt(field.get("required"));
			if (field_required)
			{
				rules.addType(field_name, new Required());
			}
		}
		
		return rules;
	}
}
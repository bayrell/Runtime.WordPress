/*!
 *  BayLang Technology
 *
 *  (c) Copyright 2016-2024 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Runtime.WordPress.Database;

use Runtime.BaseObject;
use Runtime.DateTime;
use Runtime.ORM.Record;
use Runtime.ORM.Relation;
use Runtime.ORM.Annotations.BigIntType;
use Runtime.ORM.Annotations.Primary;
use Runtime.ORM.Annotations.StringType;


class FormIP extends Relation
{
	/**
	 * Returns table name
	 */
	pure string getTableName() => "forms_ip";
	
	
	/**
	 * Returns table schema
	 */
	pure memorize Collection<BaseObject> schema() =>
	[
		/* Fields */
		new StringType{"name": "ip"},
		new BigIntType{"name": "count"},
		new BigIntType{"name": "last"},
		
		/* Index */
		new Primary{"keys": ["ip"]},
	];
	
	
	/**
	 * Find client by ip
	 */
	async Record<FormIP> findClient(string client_ip)
	{
		return this.fetchRecord(
			this.select().where("ip", "=", client_ip)
		);
	}
	
	
	/**
	 * Check spam
	 */
	async bool checkSpam(string client_ip)
	{
		int time = DateTime::now().getTimestamp();
		
		Record<FormIP> item = await this.findClient(client_ip);
		if (not item)
		{
			item = this.createRecord();
			item.count = 1;
			item.ip = client_ip;
			item.last = time;
			
			await item.save();
			
			return false;
		}
		
		bool spam_result = false;
		int count = item.count + 1;
		
		if (item.last + 15*60 > time)
		{
			if (count > 3)
			{
				spam_result = true;
			}
		}
		else
		{
			count = 1;
		}
		
		item.count = count;
		item.last = time;
		await item.save();
		
		return spam_result;
	}
}
/*!
 *  BayLang Technology
 *
 *  (c) Copyright 2016-2024 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Runtime.WordPress.Service;

use Runtime.BaseObject;
use Runtime.DateTime;
use Runtime.ORM.Relation;
use Runtime.WordPress.Database.Form;
use Runtime.WordPress.Database.FormData;
use Runtime.WordPress.Database.FormIP;
use Runtime.WordPress.Theme.Components.Email.FormMessage;
use Runtime.WordPress.EmailProvider;


class FormService extends BaseObject
{
	Relation<Form> form_relation = new Relation(classof Form);
	Relation<FormIP> form_ip_relation = new Relation(classof FormIP);
	
	
	/**
	 * Find form
	 */
	async Form findForm(string form_name)
	{
		return await this.form_relation.fetchRecord(
			this.form_relation.select().where("api_name", "=", form_name)
		);
	}
	
	
	/**
	 * Find client by ip
	 */
	async FormIP findClient(string client_ip)
	{
		return this.form_ip_relation.fetchRecord(
			this.form_ip_relation.select().where("ip", "=", client_ip)
		);
	}
	
	
	/**
	 * Check spam
	 */
	async bool checkSpam(string client_ip)
	{
		int time = DateTime::now().getTimestamp();
		
		FormIP item = await this.findClient(client_ip);
		if (not item)
		{
			item = new FormIP();
			item.count = 1;
			item.ip = client_ip;
			item.last = time;
			
			await this.form_ip_relation.save(item);
			
			return false;
		}
		
		bool spam_result = false;
		int count = item.count + 1;
		
		if (item.last + 15*60 > time)
		{
			if (count > 3)
			{
				spam_result = true;
			}
		}
		else
		{
			count = 1;
		}
		
		item.count = count;
		item.last = time;
		await this.form_ip_relation.save(item);
		
		return spam_result;
	}
	
	
	/**
	 * Send email
	 */
	async void sendEmail(
		Form form, FormData form_data,
		EmailProvider email, string site_name
	)
	{
		string email_dest = form.email_to;
		string email_title = rs::join(" ", [
			form_data.form_title,
			"from",
			site_name,
			"N" ~ form_data.id,
		]);
		await email.send({
			"dest": email_dest,
			"title": email_title,
			"component": classof FormMessage,
			"props":
			{
				"site_name": site_name,
				"form_name": form.name,
				"form_title": form_data.form_title,
				"invoice_id": form_data.id,
				"metrika_form_id": form_data.metrika_id,
				"data": form_data.data
			},
		});
	}
}
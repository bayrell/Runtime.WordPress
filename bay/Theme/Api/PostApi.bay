/*!
 *  BayLang Technology
 *
 *  (c) Copyright 2016-2024 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Runtime.WordPress.Theme.Api;

use Runtime.DateTime;
use Runtime.Serializer.Allowed;
use Runtime.Serializer.IntegerType;
use Runtime.Serializer.MapType;
use Runtime.Serializer.StringType;
use Runtime.ORM.Connection;
use Runtime.ORM.Cursor;
use Runtime.ORM.Query;
use Runtime.ORM.QueryField;
use Runtime.ORM.QueryFilter;
use Runtime.ORM.QueryResult;
use Runtime.ORM.Record;
use Runtime.ORM.Relation;
use Runtime.Web.ApiRequest;
use Runtime.Web.ApiResult;
use Runtime.Web.BaseApi;
use Runtime.Web.Annotations.ApiMethod;
use Runtime.WordPress.Database.Post;


class PostApi extends BaseApi
{
	Post relation_post = null;
	
	
	/**
	 * Returns api name
	 */
	pure string getApiName() => "runtime.wordpress.post";
	
	
	/**
	 * Create object
	 */
	void constructor()
	{
		parent();
		this.relation_post = new Post();
	}
	
	
	/**
	 * Search post
	 */
	@ApiMethod{ "name": "search" }
	async ApiResult actionSearch()
	{
		Map update_data = this.filter(new MapType{
			"page": new IntegerType(),
			"limit": new IntegerType(),
			"post_type": [
				new Allowed(["post", "page"]),
				new StringType(),
			],
			"post_status": [
				new Allowed(["publish"]),
				new StringType(),
			],
		});
		
		string post_type = update_data.get("post_type");
		string post_status = update_data.get("post_status");
		int page = update_data.get("page", 0);
		int limit = update_data.get("limit", 12);
		
		if (post_type == "") post_type = "post";
		if (post_status == "") post_status = "publish";
		
		Query q = this.relation_post.select([
				"posts.ID",
				"posts.post_content",
				"posts.post_title",
				"posts.post_name",
				"posts.post_status",
				"posts.post_type",
				"posts.post_date_gmt",
				"posts.post_modified_gmt",
			])
			.calcFoundRows()
			.where("post_type", "=", post_type)
			.where("post_status", "=", post_status)
			.page(page)
			.limit(limit)
		;
		QueryResult items = await this.relation_post.fetchAll(q);
		return this.result.success({
			"data": {
				"items": items.map(Map (Map item) => item),
				"count": items.rows,
				"limit": limit,
				"page": items.getPage(),
				"pages": items.getPages(),
			}
		})
	}
}
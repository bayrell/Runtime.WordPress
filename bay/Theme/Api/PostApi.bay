/*!
 *  BayLang Technology
 *
 *  (c) Copyright 2016-2024 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Runtime.WordPress.Theme.Api;

use Runtime.DateTime;
use Runtime.Serializer.Allowed;
use Runtime.Serializer.BaseType;
use Runtime.Serializer.IntegerType;
use Runtime.Serializer.MapType;
use Runtime.Serializer.StringType;
use Runtime.ORM.Connection;
use Runtime.ORM.Cursor;
use Runtime.ORM.Query;
use Runtime.ORM.QueryField;
use Runtime.ORM.QueryFilter;
use Runtime.ORM.QueryResult;
use Runtime.ORM.Record;
use Runtime.ORM.Relation;
use Runtime.Web.ApiRequest;
use Runtime.Web.ApiResult;
use Runtime.Web.Annotations.ApiMethod;
use Runtime.Widget.Api.SearchApi;
use Runtime.WordPress.Database.Post;


class PostApi extends SearchApi
{
	/**
	 * Returns api name
	 */
	pure string getApiName() => "runtime.wordpress.post";
	
	
	/**
	 * Returns relation name
	 */
	string getRealtionName() => classof Post;
	
	
	/**
	 * Returns data rules
	 */
	void getDataRules(BaseType rules)
	{
		parent(rules);
		rules.addType("lang", new StringType());
		rules.addType("post_type", new Allowed(["post", "page"]));
		rules.addType("post_type", new StringType{ "default": "post" });
		rules.addType("post_status", new Allowed(["publish"]));
		rules.addType("post_status", new StringType{ "default": "publish" });
	}
	
	
	/**
	 * Returns serialize rules for pk
	 */
	void getPrimaryRules(BaseType rules)
	{
		rules.addType("id", new IntegerType());
	}
	
	
	/**
	 * Returns item fields
	 */
	Map getItemFields() => [
		"ID",
		"post_content",
		"post_title",
		"post_name",
		"post_status",
		"post_type",
		"post_date_gmt",
		"post_modified_gmt",
	];
	
	
	/**
	 * Build lang
	 */
	async void buildLangQuery(Query q)
	{
		string lang = this.data.get("lang");
		if (lang == "") return;
		
		Query q_taxonomy = new Query()
			.select([
				"term_taxonomy.term_taxonomy_id"
			])
			.from("term_taxonomy")
			.innerJoin("terms", "terms.term_id = term_taxonomy.term_id")
			.where("term_taxonomy.taxonomy", "=", "language")
			.where("terms.slug", "=", lang)
		;
		
		Connection conn = Connection::get();
		Map row = await conn.fetchOne(q_taxonomy);
		if (not row) return;
		
		int term_taxonomy_id = row.get("term_taxonomy_id");
		q.innerJoin("term_relationships", "term_lang.object_id = posts.ID", "term_lang")
		q.where("term_lang.term_taxonomy_id", "=", term_taxonomy_id)
	}
	
	
	/**
	 * Build Query
	 */
	async void buildQuery(Query q)
	{
		await parent(q);
		
		string post_type = this.data.get("post_type");
		string post_status = this.data.get("post_status");
		
		q.select([
			"posts.ID",
			"posts.post_content",
			"posts.post_title",
			"posts.post_name",
			"posts.post_status",
			"posts.post_type",
			"posts.post_date_gmt",
			"posts.post_modified_gmt",
		])
		.where("post_type", "=", post_type)
		.where("post_status", "=", post_status);
		
		this.buildLangQuery(q);
	}
	
	
	/**
	 * Search post
	 */
	@ApiMethod{ "name": "search" }
	async void actionSearch()
	{
		await parent();
	}
	
	
	/**
	 * Item post
	 */
	@ApiMethod{ "name": "item" }
	async void actionItem()
	{
		await parent();
	}
}
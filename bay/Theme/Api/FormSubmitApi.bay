/*!
 *  BayLang Technology
 *
 *  (c) Copyright 2016-2024 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Runtime.WordPress.Theme.Api;

use Runtime.DateTime;
use Runtime.Exceptions.ApiError;
use Runtime.Exceptions.ItemNotFound;
use Runtime.Exceptions.FieldException;
use Runtime.Exceptions.RuntimeException;
use Runtime.Serializer.BaseType;
use Runtime.Serializer.MapType;
use Runtime.Serializer.ObjectType;
use Runtime.Serializer.Required;
use Runtime.Serializer.StringType;
use Runtime.Serializer.TypeError;
use Runtime.ORM.Connection;
use Runtime.ORM.Cursor;
use Runtime.ORM.Query;
use Runtime.ORM.QueryField;
use Runtime.ORM.QueryFilter;
use Runtime.ORM.QueryResult;
use Runtime.ORM.Relation;
use Runtime.ORM.Record;
use Runtime.Web.ApiRequest;
use Runtime.Web.ApiResult;
use Runtime.Web.BaseApi;
use Runtime.Web.Annotations.ApiMethod;
use Runtime.Widget.Api.FieldErrors;
use Runtime.WordPress.EmailProvider;
use Runtime.WordPress.Database.Form;
use Runtime.WordPress.Database.FormData;
use Runtime.WordPress.Database.FormIP;


class FormSubmitApi extends BaseApi
{
	Record form = null;
	Form relation_form = null;
	FormData relation_form_data = null;
	FormIP relation_form_ip = null;
	EmailProvider email = null;
	string client_ip = "";
	
	
	/**
	 * Returns api name
	 */
	pure string getApiName() => "runtime.wordpress.form.submit";
	
	
	/**
	 * Create object
	 */
	void constructor()
	{
		parent();
		this.relation_form = new Form();
		this.relation_form_data = new FormData();
		this.relation_form_ip = new FormIP();
		this.email = @.provider("email");
	}
	
	
	/**
	 * Set request
	 */
	void setRequest(ApiRequest request)
	{
		parent(request);
		this.client_ip = request.storage.get("client_ip");
	}
	
	
	/**
	 * Returns form settings
	 */
	@ApiMethod{ "name": "settings" }
	async ApiResult actionSettings()
	{
		/* Validate data */
		Map update_data = this.filter(this.request.data, new MapType{
			"form_name": new StringType(),
		});
		
		/* Find form */
		string form_name = update_data.get("form_name");
		Record<Form> form = await this.relation_form.findForm(form_name);
		if (not form)
		{
			throw new ApiError(new ItemNotFound(form_name, "Form"));
		}
		
		/* Get form settings */
		Map form_settings = form.settings;
		if (not form_settings)
		{
			throw new ApiError(new ItemNotFound(form_name, "Form settings"));
		}
		
		/* Set result */
		return this.result.success({
			"message": "Ok",
			"data": {
				"name": form_name,
				"settings": form_settings,
			}
		});
	}
	
	
	/**
	 * Action save
	 */
	@ApiMethod{ "name": "save" }
	async ApiResult actionSave()
	{
		Vector errors = [];
		
		/* Validate data */
		Map update_data = this.filter(this.request.data, new MapType{
			"form_name": new StringType(),
			"form_title": new StringType(),
			"form_id": new StringType(),
			"item": new Required(),
		});
		
		/* Get data */
		string site_name = "";
		string form_name = update_data.get("form_name");
		string form_title = update_data.get("form_title");
		string form_id = update_data.get("form_id");
		
		/* Site name */
		#ifcode PHP then
		$site_name = get_bloginfo("name");
		#endif
		
		/* Find form */
		Record<Form> form = await this.relation_form.findForm(form_name);
		if (!form)
		{
			throw new ApiError(new ItemNotFound(form_name, "Form"));
		}
		
		/* Validate item */
		BaseType rules = this.relation_form.getRules(form);
		Map item_data = rules.filter(update_data.get("item"), errors);
		if (not item_data or errors.count() > 0)
		{
			return this.result.fail(new FieldException({
				"fields": TypeError::getMap(errors),
			}));
		}
		
		/* Spam check */
		bool spam_check = await this.relation_form_ip.checkSpam(this.client_ip);
		if (spam_check)
		{
			return this.result.success({
				"message": "Ok",
			});
		}
		
		/* Save form */
		Record<FormData> form_data = this.relation_form_data.createRecord();
		form_data.form_id = form.id;
		form_data.form_title = form_title ? form_title : form.name;
		form_data.metrika_id = form_id ? form_id : "form";
		form_data.data = update_data.get("item");
		form_data.spam = spam_check;
		await form_data.save();
		
		/* Send email */
		if (not spam_check)
		{
			await this.relation_form_data.sendEmail(
				form, form_data, this.email, site_name
			);
		}
		
		/* Set result */
		return this.result.success({
			"message": "Ok",
		});
	}
}
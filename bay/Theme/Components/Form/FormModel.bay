/*!
 *  BayLang Technology
 *
 *  (c) Copyright 2016-2024 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Runtime.WordPress.Theme.Components.Form;

use Runtime.ApiResult;
use Runtime.Serializer.BooleanType;
use Runtime.Serializer.MapType;
use Runtime.Serializer.ObjectType;
use Runtime.Serializer.StringType;
use Runtime.Serializer.VectorType;
use Runtime.Web.RenderContainer;
use Runtime.Widget.Form.FormModel as BaseFormModel;
use Runtime.WordPress.Theme.Components.Form.Form;


class FormModel extends BaseFormModel
{
	string component = classof Form;
	string form_name = "";
	string form_title = "";
	string form_event = "";
	string form_id = "";
	string redirect_url = "";
	Vector fields = [];
	Map item = null;
	
	
	/**
	 * Serialize object
	 */
	static void serialize(ObjectType rules)
	{
		parent(rules);
		rules.addType("form_name", new StringType());
		rules.addType("form_title", new StringType());
		rules.addType("form_event", new StringType());
		rules.addType("form_id", new StringType());
		rules.addType("redirect_url", new StringType());
		rules.addType("fields", new VectorType(new MapType{
			"name": new StringType(),
			"title": new StringType(),
			"type": new StringType(),
			"placeholder": new StringType(),
			"required": new BooleanType(),
		}));
	}
	
	
	/**
	 * Init widget params
	 */
	void initParams(Dict params)
	{
		parent(params);
		
		if (params == null) return;
		if (params.has("form_name")) this.form_name = params.get("form_name");
		if (params.has("form_title")) this.form_title = params.get("form_title");
		if (params.has("form_event")) this.form_event = params.get("form_event");
		if (params.has("form_id")) this.form_id = params.get("form_id");
		if (params.has("redirect_url")) this.redirect_url = params.get("redirect_url");
	}
	
	
	/**
	 * Init widget settings
	 */
	void initWidget(Dict params)
	{
		parent(params);
	}
	
	
	/**
	 * Returns field component
	 */
	string getFieldComponent(string field_type)
	{
		if (field_type == "textarea") return "Runtime.Widget.TextArea";
		return "Runtime.Widget.Input";
	}
	
	
	/**
	 * Load data
	 */
	async void loadData(RenderContainer container)
	{
		await parent(container);
		await this.loadForm();
	}
	
	
	/**
	 * Load form
	 */
	async void loadForm()
	{
		ApiResult result = await this.layout.sendApi({
			"api_name": "runtime.wordpress.form.submit",
			"method_name": "settings",
			"data": {
				"form_name": this.form_name,
			},
		});
		if (result.isSuccess())
		{
			/* Clear fields */
			this.fields = [];
			
			/* Add new fields */
			Map settings = result.data.get("settings");
			this.fields = settings.get("fields");
		}
	}
	
	
	/**
	 * Submit form
	 */
	async ApiResult submit()
	{
		this.setWaitMessage();
		
		ApiResult result = await this.layout.sendApi({
			"api_name": "runtime.wordpress.form.submit",
			"method_name": "save",
			"data": {
				"form_name": this.form_name,
				"form_title": this.form_title,
				"form_id": this.form_id,
				"item": this.item,
			},
		});
		
		this.setApiResult(result);
		
		/* Send event */
		@.hook("runtime.wordpress::form_submit", {
			"form": this,
			"result": result,
		});
		
		/* Redirect */
		if (result.isSuccess() and this.redirect_url != "")
		{
			document.location = this.redirect_url;
		}
		
		return result;
	}
}